Class CNA.Tests Extends %UnitTest.TestCase
{

Property CNAPath As %String;

Property testLib As %String;

Property Hunspell As %String;

Property HunspellAff As %String;

Property HunspellDic As %String;

Property Ulysses As %String;

Method TestLowLevel()
{
	try {
		do $$$AssertEquals(##class(%File).Exists(..CNAPath), 1, "CNA file exists")
		do $$$AssertEquals(##class(%File).Exists(..testLib), 1, "Testlib file exists")
	
		do $zf(-3, ..CNAPath)
		
		set libID = $zf(-3, "", "load_library", ..testLib)
		
		set func = "sum"
		set argTypes = $CHAR(3, 3, 3, 3, 3, 3)
		set args = $ZLCHAR(12, 10, 35, 321, 21)
		set retVal = $zf(-3, "", "call_function", libID, func, argTypes, args)
		set retVal = $ZLASCII(retVal)
		do $$$AssertEquals(retVal, 399, "Check sum() from test library: 12 + 10 + 35 + 321 + 21 = 399")
		
		set func = "mul"
		set argTypes = $CHAR(9, 12, 12)
		set args = $ZQCHAR(10) _ $ZDCHAR($DOUBLE(1.5))
		set retVal = $zf(-3, "", "call_function", libID, func, argTypes, args)
		set retVal = $ZDASCII(retVal)
		do $$$AssertEquals(retVal, $double(15), "Check mul() from test library: 10 * 1.5 = 15")

		set func = "x"
		set argTypes = $CHAR(9, 9)
		set args = $ZQCHAR(-2384929929829)
		set retVal = $zf(-3, "", "call_function", libID, func, argTypes, args)
		set retVal =  $ZQASCII(retVal)
		do $$$AssertEquals(retVal, -2384929929829, "Check x() from test library:" _ retVal _ " = -2384929929829")
		
		set func = "square"
		set argTypes = $CHAR(12, 12)
		set args = $ZDCHAR($DOUBLE(1.2))
		set retVal = $zf(-3, "", "call_function", libID, func, argTypes, args)
		set retVal = $ZDASCII(retVal)
		do $$$AssertEquals(retVal, $double(1.44), "Check square() from test library: 1.2 ^ 2 = 1.44")
		
		do $zf(-3, "", "free_library", libID)
		
	}catch ex {

		write $system.Status.GetErrorText(ex.AsStatus(),"ru"),!
		do $zf(-3, "")
		do $$$AssertEquals(0, 1, "Exception were catched")
	}
}

Method TestAPI()
{
	try {
		do $$$AssertEquals(##class(%File).Exists(..testLib), 1, "testLib file exists")

		set cna = ##class(CNA.CNA).%New(..CNAPath)
		do cna.LoadLibrary(..testLib)
		
		set argTypes = $lb(cna.#INT64, cna.#DOUBLE)
		set retVal = cna.CallFunction("mul", cna.#DOUBLE, argTypes, 10, $double(1.5))
		do $$$AssertEquals(retVal, $double(15), "Check return value of mul()")
		
		set argTypes = $lb(cna.#DOUBLE)
		set retVal = cna.CallFunction("square", cna.#DOUBLE, argTypes, $double(12341.12311))
		do $$$AssertEquals(retVal, $double(12341.12311) * $double(12341.12311), "Check return value of square()")

		set argTypes = $lb(cna.#UINT, cna.#UINT, cna.#UINT, cna.#UINT, cna.#UINT)
		set retVal = cna.CallFunction("sum", cna.#UINT, argTypes, 1243, 1231, 121, 12123, 121)
		do $$$AssertEquals(retVal, 1243 + 1231 + 121 + 12123 + 121, "Check return value of sum()")

		set retVal = cna.CallFunction("five", cna.#INT)
		do $$$AssertEquals(retVal, 5, "Check return value of five()")

		do cna.FreeLibrary()
	}catch ex {
		do cna.FreeLibrary()
		do $$$AssertEquals(0, 1, $system.Status.GetErrorText(ex.AsStatus()))
	}
}

Method TestPointers()
{
	try {
		set cna = ##class(CNA.CNA).%New(..CNAPath)
		do cna.LoadLibrary(..testLib)
		
		
		set argTypes = $lb(cna.#SIZET)
		set memory = cna.CallFunction("mal", cna.#POINTER, argTypes, 8)
		
		do cna.PointerSetAt(memory, cna.#UINT, 0, 12345678)		
		do cna.PointerSetAt(memory, cna.#UINT, 1, 1313)
		
		set mem0 = cna.PointerGetAt(memory, cna.#UINT, 0)
		do $$$AssertEquals(mem0, 12345678, "memory[0] must be equal to 12345678")
		
		set mem1 = cna.PointerGetAt(memory, cna.#UINT, 1)
		do $$$AssertEquals(mem1, 1313, "memory[1] must be equal to 1313")
		
		set argTypes = $lb(cna.#POINTER)
		do cna.CallFunction("fr", cna.#VOID, argTypes, memory)
		
		set testString = "hi there, i'm testing string."
		set p = cna.ConvertStringToPointer(testString)
		set s = cna.ConvertPointerToString(p)
		do $$$AssertEquals(testString, s, "Check convesrsion strings to/from pointers")
		
		do cna.FreeLibrary()
	}catch ex {
		do cna.FreeLibrary()
		do $$$AssertEquals(0, 1, $system.Status.GetErrorText(ex.AsStatus()))
	}
}

Method TestIntegers()
{
	try {
		set cna = ##class(CNA.CNA).%New(..CNAPath)
		do cna.LoadLibrary(..testLib)
		
		set argTypes = $lb(cna.#UCHAR)
		set retVal = cna.CallFunction("_uchar", cna.#UCHAR, argTypes, $zhex("FD"))
		do $$$AssertEquals(retVal, $zhex("FD"), "Check uchar() return value")
		
		set argTypes = $lb(cna.#SCHAR)
		set retVal = cna.CallFunction("_schar", cna.#SCHAR, argTypes, -3)
		do $$$AssertEquals(retVal, -3, "Check schar() return value")
		
		set argTypes = $lb(cna.#USHORT)
		set retVal = cna.CallFunction("_ushort", cna.#USHORT, argTypes, $zhex("FFFD"))
		do $$$AssertEquals(retVal, $zhex("FFFD"), "Check ushort() return value")
		
		set argTypes = $lb(cna.#SHORT)
		set retVal = cna.CallFunction("_sshort", cna.#SHORT, argTypes, -3)
		do $$$AssertEquals(retVal, -3, "Check sshort() return value")
		
		set argTypes = $lb(cna.#UINT)
		set retVal = cna.CallFunction("_uint", cna.#UINT, argTypes, $zhex("FFFFFFFD"))
		do $$$AssertEquals(retVal, $zhex("FFFFFFFD"), "Check uint() return value")
		
		set argTypes = $lb(cna.#INT)
		set retVal = cna.CallFunction("_sint", cna.#INT, argTypes, -3)
		do $$$AssertEquals(retVal, -3, "Check sint() return value")

		
		set argTypes = $lb(cna.#ULONG)
		set retVal = cna.CallFunction("_ulong", cna.#ULONG, argTypes, $zhex("FFFFFFFD"))
		do $$$AssertEquals(retVal, $zhex("FFFFFFFD"), "Check ulong() return value")
		
		set argTypes = $lb(cna.#LONG)
		set retVal = cna.CallFunction("_slong", cna.#LONG, argTypes, -3)
		do $$$AssertEquals(retVal, -3, "Check slong() return value")

		
		set argTypes = $lb(cna.#UINT64)
		set retVal = cna.CallFunction("_uint64", cna.#UINT64, argTypes, 1234567890)
		do $$$AssertEquals(retVal, 1234567890, "Check uint64() return value")
		
		set argTypes = $lb(cna.#INT64)
		set retVal = cna.CallFunction("_sint64", cna.#INT64, argTypes, -3)
		do $$$AssertEquals(retVal, -3, "Check sint64() return value")
		
		do cna.FreeLibrary()
	}catch ex {
		do cna.FreeLibrary()
		do $$$AssertEquals(0, 1, $system.Status.GetErrorText(ex.AsStatus()))
	}
}

Method TestStructures()
{
	try {
		set cna = ##class(CNA.CNA).%New(..CNAPath)
		do cna.LoadLibrary(..testLib)
		
		set structType = $lb(cna.#SCHAR, cna.#INT64, cna.#SCHAR)
		set structVal = $lb(12, 2114, 74)
		set retVal = cna.CallFunction("sum_st", cna.#INT64, $lb(structType), structVal)
		do $$$AssertEquals(retVal, 2200, "Check return value of sum_st()")
		
		
		set retVal = cna.CallFunction("create_st", structType,)
		do $$$AssertEquals($LIST(retVal, 1), 1, "Check return value of create_st()")
		do $$$AssertEquals($LIST(retVal, 2), 2, "Check return value of create_st()")
		do $$$AssertEquals($LIST(retVal, 3), 3, "Check return value of create_st()")
		
		
		set nestedType = $lb(cna.#SCHAR, cna.#INT64, cna.#SCHAR)
		set argTypes = $lb($lb(cna.#SCHAR, nestedType, cna.#SCHAR))
		
		set structVal = $lb(12, $lb(-14, 33100, 28), 74)
		
		set retVal = cna.CallFunction("sum_nested", cna.#INT64, argTypes, structVal)
		do $$$AssertEquals(retVal, 33200, "Check return value of sum_nested()")
		
			
		do cna.FreeLibrary()
	}catch ex {
		do cna.FreeLibrary()
		do $$$AssertEquals(0, 1, $system.Status.GetErrorText(ex.AsStatus()))
	}
}

Method TestFloatingPoint()
{
	try {
		set cna = ##class(CNA.CNA).%New(..CNAPath)
		do cna.LoadLibrary(..testLib)
		
		set argTypes = $lb(cna.#FLOAT, cna.#FLOAT)
		set retVal = cna.CallFunction("sum_float", cna.#FLOAT, argTypes, $double(0.2), $double(1.5))
		do $$$AssertEquals($zabs(retVal - $double(1.7)) < $double(0.0000001), 1, "Check return value of sum_float()")
		
		if ($SYSTEM.Version.GetOS() = "UNIX") {
			set argTypes = $lb(cna.#LONGDOUBLE, cna.#LONGDOUBLE)
			set retVal = cna.CallFunction("sum_long_double", cna.#LONGDOUBLE, argTypes, $double(0.2), $double(1.5))
			do $$$AssertEquals(retVal, $double(1.7), "Check return value of sum_long_double()")
		}
		
		do cna.FreeLibrary()
	}catch ex {
		do cna.FreeLibrary()
		do $$$AssertEquals(0, 1, $system.Status.GetErrorText(ex.AsStatus()))
	}
}

Method TestStreams()
{
	try {
		set cna = ##class(CNA.CNA).%New(..CNAPath)
		do cna.LoadLibrary(..testLib)
		
		set file = ##class(%Stream.FileBinary).%New()	
		do file.LinkToFile(..Ulysses)
		
		set pointer = cna.ConvertStreamToPointer(file)
		set path = cna.ConvertStringToPointer(..Ulysses)
			
		set argTypes = $lb(cna.#POINTER, cna.#POINTER)
		
		set retVal = cna.CallFunction("compare_string_and_ulysses", cna.#INT, argTypes, pointer, path)
		do $$$AssertEquals(retVal, 1, "Check retval")
		
		do cna.FreeLibrary()
	}catch ex {
		do cna.FreeLibrary()
		do $$$AssertEquals(0, 1, $system.Status.GetErrorText(ex.AsStatus()))
	}
}

Method OnBeforeAllTests() As %Status
{
	if ($SYSTEM.Version.GetOS() = "UNIX") {
		set delim = "/"
		set suffix = ".so"
	} else {
		set delim = "\"
		set suffix = ".dll"
	}
	set ..CNAPath = ^UnitTestRoot _ delim _ ".." _ delim _ "libcna" _ suffix
	set ..testLib = ^UnitTestRoot _ delim _ "libtest" _ suffix
	set hunspellDir = ^UnitTestRoot _ delim _ ".." _ delim _ ".." _ delim _ "hunspell" _ delim 
	set ..Hunspell = hunspellDir _ "libhunspell" _ suffix
	set ..HunspellAff = hunspellDir _ "ru_RU.aff"
	set ..HunspellDic = hunspellDir _ "ru_RU.dic"
	
	set ..Ulysses = ^UnitTestRoot _ delim _ "ulysses.txt"

 	Quit $$$OK
}

}

